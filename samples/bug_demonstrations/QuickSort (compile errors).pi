typedef struct qs {
	int pivot;
	int[] array;
} qs;

@pre  true
@post sorted(rv, 0, |rv| - 1)
int[] QuickSort(int[] a)
{
	return qsort(a, 0, |a| - 1);
}

@pre  true
@post true
int[] qsort(int[] a, int l, int u) {
	if (l >= u) 
		return a;
	else {
		qs p := partition(a, l, u);
		a := p.array;
		a := qsort(a, l, p.pivot - 1);
		a := qsort(a, p.pivot + 1, u);
		return a;
  }
}

@pre  true
@post true
qs partition(int[] a, int l, int u) {
	int pi := random(l, u);
	int pv := a[pi];
	a[pi] := a[u];
	a[u] := pv;

	int i := l - 1;
	for
		@ true
		(int j := l; j < u; j := j + 1)
	{
		if (a[j] <= pv) {
			i := i + 1;
			int t := a[i];
			a[i] := a[j];
			a[j] := t;
		}
	}

	int t := a[i + 1];
	a[i + 1] := a[u];
	a[u] := t;

	qs tmp;
	tmp.pivot := i + 1;
	tmp.array := a;
	return tmp;
}

@pre  l <= u
@post l <= rv && rv <= u
int random(int l, int u)
{
	/* a placeholder: only the @post is important */
	return u;
}
